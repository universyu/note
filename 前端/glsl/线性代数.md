# 线性代数

## 坐标变换

从顶点坐标变成齐次坐标然后乘以模型矩阵获得世界坐标，乘以视角矩阵切换到相机坐标，乘以投影矩阵把`3D`空间投影到`2D`空间

```glsl
precision highp float;

attribute vec3 position;

uniform mat4 model, view, projection;

void main() {
  gl_Position = projection * view * model * vec4(position, 1);
}
```

## 平移操作

位置为`p`，将齐次位置（  `vec4( p , 1 )`  ）前乘以一个

```glsl
mat4(1, 0, 0, 0,
     0, 1, 0, 0, 
     0, 0, 1, 0,
     -p.x, -p.y, -p.z, 1)
```

即可将其移动到原点。

这里的 mat4 每一行是一个 vec4 ， 而 vec4 是列向量

## 反射矩阵

求向量关于法向量为`n`的平面的反射向量，向量与反射向量在`n`上的投影向量反向

```glsl
vec3 reflectPoint(vec3 p, vec3 n) {
  return p - 2.0 * dot(n, p) * n / dot(n, n);
}
```

`normalize`将每个轴的值都除以模长，将反射构建成矩阵

```glsl
highp mat4 reflection(highp vec3 n) {
  n = normalize(n);
  return mat4(1.0-2.0*n.x*n.x,    -2.0*n.y*n.x,    -2.0*n.z*n.x, 0,
                 -2.0*n.y*n.x, 1.0-2.0*n.y*n.y,    -2.0*n.z*n.y, 0, 
                 -2.0*n.z*n.x,    -2.0*n.y*n.z, 1.0-2.0*n.z*n.z, 0,
                            0,               0,               0, 1);
}
```

## 线代恒等式

$$
点乘转矩阵乘法：\vec{B}·\vec{A}·\vec{B} = BB^TA\\
\vec{A}在\vec{B}的投影向量：\vec{B}·\vec{A}·\vec{B}\ / \ |\vec{B}|^2\\
叉乘转矩阵乘法：\vec{A}\ x\ \vec{B} = aB \  其中a=\begin{bmatrix} 0&-A_{z}&A_{y}\\A_{z}&0&-A_{x}\\-A_{y}&A_{x}&0 \end{bmatrix}  \\
Rodrigues旋转公式：v_{rot}=Rv \  其中\ R = I+sin\theta K + (1-cos\theta)K^2 ,\ K是旋转轴单位向量生成的反对称阵
$$

绕着单位轴`n`转角`theta`的矩阵

```glsl
highp mat4 rotation(highp vec3 n, highp float theta) {

  float s = sin(theta);
  float c = cos(theta);
  float oc = 1.0 - c;

  return mat4(
    oc * n.x * n.x + c,       oc * n.x * n.y + n.z * s,   oc * n.z * n.x - n.y * s,   0.0,
    oc * n.x * n.y - n.z * s, oc * n.y * n.y + c,         oc * n.y * n.z + n.x * s,   0.0,
    oc * n.z * n.x + n.y * s, oc * n.y * n.z - n.x * s,   oc * n.z * n.z + c,         0.0,
    0.0,                      0.0,                        0.0,                        1.0
  );
}
```

